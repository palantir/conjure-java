/*
 * (c) Copyright 2018 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import static org.apache.tools.ant.taskdefs.condition.Os.*

ext {
    osClassifier = isFamily(FAMILY_MAC) ? "osx" : "linux"
}

// We locally-codegen verification-api.conjure.json into these source-sets
sourceSets {
    specObjects
    specJersey
}

configurations {
    testCases
    verificationApi
    verificationServer
}

idea {
    module {
        sourceDirs += sourceSets.specObjects.java.srcDirs
        sourceDirs += sourceSets.specJersey.java.srcDirs
        scopes.COMPILE.plus += [configurations.specObjectsCompile, configurations.specJerseyCompile]
    }
}

dependencies {
    testCases 'com.palantir.conjure.verification:test-cases'
    verificationApi 'com.palantir.conjure.verification:verification-api'
    verificationServer "com.palantir.conjure.verification:verification-server::${osClassifier}@tgz"

    specObjectsCompile project(':conjure-java-core')
    specObjectsCompile project(':conjure-lib')

    specJerseyCompile project(':conjure-java-core')
    specJerseyCompile project(':conjure-lib')
    specJerseyCompile sourceSets.specObjects.output

    testCompile project(':conjure-java-core')
    testCompile 'junit:junit'
    testCompile 'org.assertj:assertj-core'
    testCompile 'org.mockito:mockito-core'
    testCompile 'com.palantir.remoting3:jaxrs-clients'
    testCompile 'io.dropwizard:dropwizard-testing'
    testCompile sourceSets.specObjects.output
    testCompile sourceSets.specJersey.output

    processor 'org.immutables:value'
}

tasks.compileTestJava {
    options.compilerArgs += ['-Xep:Slf4jLogsafeArgs:OFF']
}

task unpackVerificationServer(type: Sync) {
    from { tarTree(configurations.verificationServer.singleFile) }
    into "${buildDir}/verification-server"
    rename { "server" }
}

task copyTestCases(type: Sync) {
    from configurations.testCases
    into "$buildDir/test-cases"
    rename { "test-cases.json" }
}

task conjureJavaObjects(type: JavaExec) {
    main = "com.palantir.conjure.java.cli.ConjureJavaCli"
    classpath = project(':conjure-java').sourceSets.main.runtimeClasspath
    args 'generate',  "${-> configurations.verificationApi.singleFile}", 'src/specObjects/java', '--objects'

    inputs.file "${-> configurations.verificationApi.singleFile}"
    outputs.files "src/specObjects/java"
    doFirst { delete "src/specObjects/java/*" }
}
compileSpecObjectsJava.dependsOn conjureJavaObjects

task conjureJavaJersey(type: JavaExec) {
    main = "com.palantir.conjure.java.cli.ConjureJavaCli"
    classpath = project(':conjure-java').sourceSets.main.runtimeClasspath
    args 'generate',  "${-> configurations.verificationApi.singleFile}", 'src/specJersey/java', '--jersey'

    inputs.file "${-> configurations.verificationApi.singleFile}"
    outputs.files "src/specJersey/java"
    doFirst { delete "src/specJersey/java/*" }
}
compileSpecJerseyJava.dependsOn conjureJavaJersey

tasks.idea.dependsOn conjureJavaObjects, conjureJavaJersey, unpackVerificationServer, copyTestCases
test.dependsOn unpackVerificationServer, copyTestCases
