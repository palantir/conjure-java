/*
 * (c) Copyright 2018 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import static org.apache.tools.ant.taskdefs.condition.Os.*

ext {
    osClassifier = isFamily(FAMILY_MAC) ? "osx" : "linux"
}

apply plugin: 'com.palantir.conjure-java-local'

conjure {
    java {
        addFlag 'useImmutableBytes'
        addFlag 'jersey'
        addFlag 'objects'
    }
}

configurations.conjureJava.exclude group: 'com.palantir.conjure.java'
dependencies {
    conjureJava files(project(':conjure-java').tasks.distTar.archiveFile) {
        builtBy project(':conjure-java').tasks.distTar
    }
}

configurations {
    testCases
    verifier
}

task unpackVerifier(type: Sync) {
    from { tarTree(configurations.verifier.singleFile) }
    into "${buildDir}/verification"
    rename { "verifier" }
}

task copyTestCases(type: Sync) {
    from configurations.testCases, {
        rename { "test-cases.json" }
    }
    from configurations.conjure, {
        rename { "verification-api.json" }
    }
    into "$buildDir/test-cases"
}

tasks.idea.dependsOn  unpackVerifier, copyTestCases
test.dependsOn unpackVerifier, copyTestCases
